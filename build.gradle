plugins {
    id 'antlr'
    id 'idea'
    id 'maven-publish'
}

group = 'dev.qilletni.impl'
version = '1.0.0-SNAPSHOT'

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // SLF4J API dependency
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.22.1'

    implementation 'org.antlr:antlr4-runtime:4.13.1'
    antlr 'org.antlr:antlr4:4.13.1'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'dev.qilletni.api:qilletni-api:1.0.0-SNAPSHOT'
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

generateGrammarSource {
    var pkg = 'dev.qilletni.impl.antlr'
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/main/${pkg.split("\\.").join("/")}".toString())
    maxHeapSize = "128m"
    arguments += ['-visitor', '-no-listener']
}

compileJava.dependsOn generateGrammarSource

sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main/'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()

        maven {
            url = "https://maven.pkg.github.com/rubbaboy/Qilletni"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    java {
        withSourcesJar()
        sourceCompatibility = '22'
        targetCompatibility = '22'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        if (name != "qilletni-provider-demo") {
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/rubbaboy/Qilletni"
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            }
        }
    }
}

tasks.named('sourcesJar').configure {
    dependsOn 'generateGrammarSource'
}

test {
    useJUnitPlatform()
}
